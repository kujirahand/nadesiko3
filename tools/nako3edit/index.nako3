!「../../src/plugin_express.js」を取り込む。
# ---------------------------------
# なでしこ3のユーザーフォルダ
# ---------------------------------
HOME=ホームディレクトリ取得
USER_DIR=「{HOME}/nadesiko3_user」
もし、!(USER_DIRのフォルダ存在)ならば
  USER_DIRにフォルダ作成
ここまで

# ---------------------------------
# WEBサーバ起動
# ---------------------------------
APPKEY = (65535の乱数)&「_」&(65535の乱数)&「_」&(65535の乱数)
PORT=3030
PORTでWEBサーバ起動した時には
    URL=「http://localhost:{PORT}?appkey={APPKEY}」
    「>>> --------------------------------------------------」を表示
    「>>> サーバ起動に成功しました。」を表示
    「>>> nako3edit にアクセスするには、以下のURLにアクセスします」を表示
    「>>> [URL] {URL}」を表示。
    「>>>」を表示
    ルーティング設定。
    URLをブラウザ起動。
ここまで

WEBサーバ起動失敗した時には
　　PORT = PORT + 1
　　もし、PORT> 3050ならば
　　　　「WEBサーバが起動できませんでした。」と表示。
    　　戻る。
　　ここまで
　　「- PORT変更={PORT}」と表示。
　　PORTでWEBサーバ起動
ここまで。

●ルーティング設定とは
    # ---------------------------------
    # 静的ディレクトリの指定
    # ---------------------------------
    「/html」を「{母艦パス}/html」へWEBサーバ静的パス指定
    母艦パスを「../../release」で相対パス展開してRELEASEに代入。
    「/release」をRELEASEへWEBサーバ静的パス指定。
    # ---------------------------------
    # ルーティング
    # ---------------------------------
    「/」へWEBサーバGETした時には
      「- GET」を表示。
      もし、GETデータ["appkey"]＝APPKEYならば
        URL_FILE=「/html/files.html?appkey={APPKEY}」
        URL_FILEへWEBサーバリダイレクト。
        「ファイル表示ページへ移動 => {URL_FILE}」と表示。
      違えば
        「サーバから提供されたappkeyを正しくセットしてください。」をWEBサーバ出力。
      ここまで。
    ここまで
    # ---------------------------------
    「/test」へWEBサーバGETした時には
      「test」をWEBサーバ出力。
    ここまで
    
    # ---------------------------------
    「/files」へWEBサーバGETした時には
      もし、GETデータ["appkey"]≠APPKEYならば
        「["ERROR:APPKEYをセットしてください。"]」をWEBサーバ出力。
      違えば
        S=「{USER_DIR}/*.nako3」のファイル列挙
        SをJSONエンコードしてWEBサーバ出力。
      ここまで。
    ここまで
    
    # ---------------------------------
    「/load」へWEBサーバGETした時には
      GETデータを表示。
      FILE=GETデータ["file"]
      UKEY=GETデータ["appkey"]
      もし、UKEY≠APPKEYならば
        『### APPKEYが違います ###』とWEBサーバ出力。
      ここまで
      FILE=FILEの「..」を「」に置換して、「/」を「」に置換。
      FPATH=「{USER_DIR}/{FILE}」
      FPATHが存在
      もし、そうならば
        FPATHを開く。
        それをWEBサーバ出力。
      違えば
        『# --- ここにプログラムを記述 ---』&改行&『「こんにちは」と表示。』をWEBサーバ出力。
      ここまで。
    ここまで。
    
    # ---------------------------------
    「/run」へWEBサーバPOSTした時には
      はいでサーバ保存実行処理。
    ここまで
    
    # ---------------------------------
    「/save」へWEBサーバPOSTした時には
      いいえでサーバ保存実行処理。
    ここまで
ここまで。

●サーバ保存実行処理(RUNで)
    FILE=POSTデータ["file"]
    BODY=POSTデータ["body"]
    UKEY=POSTデータ["appkey"]
    もし、UKEY≠APPKEYならば
    「エラー:APPKEYが違います」をWEBサーバ出力。
    戻る。
    違えば
    ここまで。
    もし、(FILE=undefined)または(FILE=空)ならば
    「エラー: ファイル名がありません」をWEBサーバ出力。
    戻る。
    ここまで
    FPATH=「{USER_DIR}/{FILE}」
    「(保存)FILE=」&FPATHを表示。
    FPATHへBODYを保存。
    FPATH=「{USER_DIR}/{FILE}」
    エラー監視
    FPATHへBODYを保存。
    エラーならば
    「NG」をWEBサーバ出力。
    戻る。
    ここまで。
    もし、RUNがはいならば
    エラー監視
      「RUN {FPATH}」と表示。
      RES=「cnako3 "{FPATH}"」を起動待機。
      「---」と表示。
      RESをWEBサーバ出力。
    エラーならば
      「[ERROR] {エラーメッセージ}」をWEBサーバ出力。
    ここまで
    違えば
    「OK」をWEBサーバ出力。
    ここまで。
ここまで。
