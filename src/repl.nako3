# ------------------------------------
# ■ REPL - なでしこ対話実行環境
# ------------------------------------
# OSごとのメッセージを決める
FOS=OS取得
もし、FOSが『win32』ならば
　　林＝「■」
　　Fエラー＝「{林} × エラーが起きました。{改行}→」
　　F区切り線＝「----------------------------------」
　　FOK＝「○ 実行しました」
違えば
　　林＝「🍉」
　　Fエラー＝「❎ エラーが起きました。{改行}→」
　　F区切り線＝「🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴🌴」
　　FOK=「✅ 実行しました」
ここまで。
F区切り線を表示。
「{林} なでしこ {ナデシコバージョン} - 対話実行環境」と表示。
「{林} OS: {FOS}」と表示。
F区切り線を表示。

MSG＝「{林} 以下にプログラムを入力{改行}」
HELP＝林&『使い方
プログラムを入力して[RETURN]キー2回押すとプログラムが実行されます。
行頭に「>」と書くと、計算結果を画面に出力します。
「終わる」と書くと、対話実行環境を終了します。
』
HELPを表示。
MSGを表示。

空行回数＝0
キャッシュ＝「」
●REPL実行(S)
　　S=トリム(S)
　　もしSが空ならば
　　　　空行回数＝空行回数＋1
　　ここまで。
　　Sが「/^[>＞]/」に正規表現マッチ
　　もし、そうならば
　　　　Sの「/^[>＞](.+)$/」を「表示($1)」に正規表現置換してSに代入。
　　　　空行回数＝2
　　ここまで。
　　キャッシュ＝キャッシュ＆S＆改行。
　　もし、空行回数が1以上ならば
　　　　「{改行}{FOK}{改行}」を表示。
　　　　キャッシュをナデシコ続ける
　　　　改行＆F区切り線を表示。
　　　　キャッシュ＝「」
　　　　空行回数＝0
　　　　MSGを表示
　　ここまで
ここまで

(関数(S)
　　エラー監視
　　　　REPL実行(S)
　　エラーならば
　　　　「{Fエラー} {エラーメッセージ}」を表示。
　　　　F区切り線を表示。
      MSGを表示。
　　　　キャッシュ＝「」
　　ここまで
ここまで)を標準入力取得した時
