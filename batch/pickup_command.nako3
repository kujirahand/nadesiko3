# --- 基本パスを取得 ---
母艦パス取得してパス抽出して基本パスに代入。
プラグイン一覧は[
    'plugin_system.js',
    'plugin_browser.js',
    'plugin_turtle.js',
    'plugin_node.js'
 ]

関数定義とは変数
関数説明とは変数
助詞定義JSONとは変数
ヨミガナとは変数


●定数定義処理(抽出文字列で)
　　定数定義＝トリム(抽出文字列[0])
　　抽出文字列[1]をトリム。
　　「const」を「定数」に置換
　　「var」を「変数」に置換して、Fタイプに代入。
　　定数値＝トリム（抽出文字列[2]）
　　ヨミガナ＝トリム（抽出文字列[3]）
　　もし、ヨミガナが空ならば
　　　　ヨミガナ＝定数定義。
　　ここまで。
　　「| {Fタイプ} | {定数定義} | | {定数値} | {ヨミガナ} |」を表示。
ここまで。

●毎行処理（Sを)
    SをトリムしてＳに代入。
    # 見出し語？
    Ｓを「/^\/\/\s*@\s*(.+)\s*$/」で正規表現マッチ
    もし、そうならば
        見出し＝抽出文字列[0]
        「●{見出し}」を表示
        戻る
    ここまで。

    ＃定数でヨミガナ付き
    Ｓを「/'(.+?)'.+(const|var).+value:\s*(.+)\}.+?\/\/\s?@?(.*)/」で正規表現マッチ
    もし、そうならば
        抽出文字列で定数定義処理。戻る。
    ここまで。
    # 定数ヨミガナ抜け＞エラー
    Ｓを「/"(.+?)".+(const|var).+value:\s*(.+)\}/」で正規表現マッチ
    もし、そうならば
        定数定義＝抽出文字列[0]
        「--- ERROR --- 『｛定数定義｝』でヨミガナがありません。」と表示
        終わる。
    ここまで。

    # 関数定義？
    Ｓを「/'(.+?)'\s*:\s*\{\s*\/\/\s*@\s*(.+)$/」で正規表現マッチ
    もし、そうならば
        助詞定義JSON=[]
        ヨミガナ＝「」
        関数定義＝トリム(抽出文字列[0])
        関数説明＝トリム(抽出文字列[1])
        関数説明で「// @」が何文字目
        もし、それ＞０ならば
        　　関数説明を「// @」で区切って、Aに代入。
        　　関数説明＝トリム(A[0])
        　　ヨミガナ＝トリム(A[1])
        　　もし、ヨミガナが空ならば、ヨミガナ＝関数定義。
        ここまで
        もし、ヨミガナが空ならば
        　　「--- ERROR ---『{関数定義}』でヨミガナがありません」と表示。
        　　終わる。
        ここまで。
        戻る。
    ここまで

    もし、関数定義≠空ならば
        Ｓを「/type\s*\:\s*'func'/」で正規表現マッチ
        もし、そうならば
        　　戻る
        ここまで。
        Ｓを「/josi\s*\:\s*(.+),$/」で正規表現マッチ
        もし、そうならば
            それ＝抽出文字列[0]
            トリム。
            「'」を「"」に置換。
            JSONデコード。
            助詞定義JSONに代入。
            戻る。
        ここまで
        Sを「/^fn\s*\:\s*function\s*\((.*?)\)/」で正規表現マッチ。
        もし、そうならば
            助詞定義＝「」
            それ＝抽出文字列[0]
            「/\,\s*sys/」を「」に正規表現置換してトリム。
            引数一覧=[]
            「,」で区切る
            反復
                大文字変換してトリムして、引数名に代入。
                引数一覧に引数名を配列追加。
            ここまで
            AA=[]
            Iを0から9まで繰り返す
                SS=「」
                NOを0から9まで繰り返す
                    もし、NO>=配列要素数(引数一覧)ならば、続ける。
                    もし、NO>=配列要素数(助詞定義JSON)ならば、続ける。
                    もし、I>=配列要素数(助詞定義JSON[NO])ならば、続ける。
                    ARG＝引数一覧[NO]
                    JOSI＝助詞定義JSON[NO][I]
                    SS=SS+ARG+JOSI
                ここまで;
                もし、SS≠空ならば、AAにSSを配列追加。
            ここまで
            AAを「/」で配列結合して、助詞定義に代入。
            「| 関数 | {関数定義} | {助詞定義} | {関数説明} | {ヨミガナ}」を表示
            関数定義＝「」
        ここまで。
    ここまで
    関数定義＝「」
    定数定義＝「」
ここまで。

●プラグイン要約作成(Fを)
    Ｆからファイル名抽出してプラグイン名に代入。
    「{改行}■{プラグイン名}」を表示。
    Ｆを開く
    改行で区切る
    ここから反復
        毎行処理。
    ここまで
ここまで。

プラグイン一覧を反復
 　　パスは「{基本パス}/src/{それ}」
 　　パスをプラグイン要約作成
 　　表示ログを「{基本パス}/batch/command.txt」に保存。
ここまで
